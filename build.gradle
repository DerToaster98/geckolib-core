buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:6.3.0"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'io.freefair.lombok'

group project.group
def snapshotVersion = System.getenv("GITHUB_RUN_NUMBER")
version = snapshotVersion == null ? project.version : "SNAPSHOT-" + snapshotVersion
archivesBaseName = project.archivesBaseName

repositories {
    mavenCentral()
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven {
    	url "https://maven.cloudsmith.io/geckolib3/geckolib/"
    }
}

dependencies {
    implementation 'com.eliotlash.molang:molang:18'
    implementation 'com.eliotlash.mclib:mclib:18'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4'
}

jar {
    from sourceSets.main.allSource
    manifest {
        attributes([
                "FMLModType"              : "LIBRARY",
                "Specification-Title"     : "geckolib",
                "Specification-Vendor"    : "geckolib",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "geckolib",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifactId = project.archivesBaseName
        }
    }
}

